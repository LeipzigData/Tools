Ein paar Beispiel-Anfragen:


PREFIX ldo: <http://leipzig-data.de/Data/Ort/>
PREFIX ldp: <http://leipzig-data.de/Data/Person/>
PREFIX ldtag: <http://leipzig-data.de/Data/Tag/>
PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

---------------------------------------------------
Suchen. Weitere Suchanfragen ganz unten

Alle MINT-Orte finden

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ldtag: <http://leipzig-data.de/Data/Tag/>
SELECT ?l ?k WHERE {
  ?p ld:hasTag ldtag:MINT .
  ?p a ld:Ort .
  ?p rdfs:label ?l .
  ?p ld:Kurzinformation ?k .
} order by (?l)

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ldtag: <http://leipzig-data.de/Data/Tag/>
SELECT distinct ?p ?l ?k WHERE {
  ?p ld:hasTag ldtag:MINT .
  ?p rdfs:label ?l .
  ?p ld:hasStatus ?k .
} order by (?l)

Alle MINT-Orte und zugehörige Projekte finden

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ldtag: <http://leipzig-data.de/Data/Tag/>
SELECT ?l ?k WHERE {
  ?p ld:hasTag ldtag:MINT .
  ?p a ld:Ort .
  ?p rdfs:label ?l .
  ?p ld:Kurzinformation ?k .
}

Alle Angebote der LSGM finden

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT ?l ?k 
WHERE {
  ?p ld:relatedBundle <http://leipzig-data.de/Data/Ort/LSGM> .
  ?p a ld:Angebot .
  ?p rdfs:label ?l .
  ?p ld:Kurzinformation ?k .
}

Alles zu MINT-Orten extrahieren

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ldtag: <http://leipzig-data.de/Data/Tag/>
construct { ?s ?p ?o . } 
WHERE {
  ?s ld:hasTag ldtag:MINT .
  ?s a ld:Ort .
  ?s ?p ?o .
}  

Alle Planungsräume

PREFIX ld: <http://leipzig-data.de/Data/Model/>
construct 
 ?s ld:containsOrtsteil ?o .
WHERE {
  ?o ld:zuPlanungsRaum ?s .
}

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT distinct ?s ?sl WHERE {
  ?o ld:zuPlanungsRaum ?s .
optional { ?s rdfs:label ?sl .}
}

Personen nach Namen finden

PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
SELECT ?p ?n WHERE {
  ?p a foaf:Person .
  ?p foaf:name ?n .
  filter regex(?n,"Meier") .
}

Träger nach Namen finden

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT ?p ?n WHERE {
  ?p a ld:Traeger .
  ?p rdfs:label ?n .
  filter regex(?n,"Meier") .
}

---------------------------------------------------
ld:hasURL durchsehen, welche noch Literale sind

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT ?p ?n WHERE {
  ?p ld:hasURL ?n .
filter isliteral(?n) .
}

---------------------------------------------------
Events kontrollieren

Welche Events haben keinen Ort?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#> 
SELECT distinct ?e WHERE {
  ?e a ld:Event .
  optional {?e ical:location ?o .}
  filter (!bound(?o))
} 

Keine - alle Events mit unbestimmten Ort haben entweder
http://leipzig-data.de/Data/Ort/NN oder
http://leipzig-data.de/Data/Ort/Stadtweit

Welche Events haben einen Ort, der nicht existiert? 

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#> 
SELECT distinct ?e ?o WHERE {
  ?e a ld:Event .
  ?e ical:location ?o .
  optional {?o rdfs:label ?l .}
  filter (!bound(?l))
} 

Keine - die unbestimmten Orte
http://leipzig-data.de/Data/Ort/NN und
http://leipzig-data.de/Data/Ort/Stadtweit
sind unter Orte.ttl erfasst

Welche Events haben einen Ort ohne Adresse?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#> 
SELECT distinct ?e ?o WHERE {
  ?e a ld:Event .
  ?e ical:location ?o .
  optional { ?o ld:hasAddress ?a .}
  filter (!bound(?a))
} 

Wie viele Adressen gibt es?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT count(distinct ?a) WHERE {
  ?e a ld:Adresse .
} 

Nur Events zu den unbestimmten Orten
http://leipzig-data.de/Data/Ort/NN und
http://leipzig-data.de/Data/Ort/Stadtweit

Welche Events haben einen Ort mit Adresse, aber ohne Geokoordinaten?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#> 
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 
SELECT distinct ?o ?a WHERE {
  ?e a ld:Event .
  ?e ical:location ?o .
  ?o ld:hasAddress ?a . 
  optional { ?a geo:lat ?g .}
  filter (!bound(?g))
} 

Welche Events vor einem bestimmten Datum?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#> 
SELECT distinct ?e ?d WHERE {
  ?e a ld:Event .
  ?e ical:dtstart ?d . 
  Filter(xsd:date(?d) <= "2013-11-01"^^xsd:date) .
} order by (xsd:date(?d))

Welche Events vor einem bestimmten Datum?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#> 
SELECT distinct ?e ?d WHERE {
  ?e a ld:Event .
  ?e ical:dtstart ?d . 
} order by (xsd:date(?d))

---------------------------------------------------
Adressen und Geokoordinaten kontrollieren

Welche Orte haben keine gültige Adresse?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX jsp: <http://leipzig-data.de/Data/Jugendstadtplan/>
SELECT distinct ?e ?o WHERE {
  ?e a jsp:Ort .
  ?e ld:hasAddress ?o .
  optional {?o rdfs:label ?l .}
  filter (!bound(?l))
} 

Wie viele Adressen?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT count(?e) WHERE {
  ?e a ld:Adresse .
} 

Ausschnitt von Adressen mit Geokoordinaten

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geonames: <http://www.geonames.org/ontology#> 
SELECT distinct ?a ?long ?lat ?way WHERE {
  ?a a ld:Adresse .
  ?a geo:lat ?lat . 
  ?a geo:long ?long . 
  ?a geonames:nearbyFeatures ?way .
  filter regex(?a, "Tierkliniken")
} 

Welche Geokoordinaten tauchen mehrfach auf?

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
SELECT distinct ?lat ?long count(distinct ?e) as ?s WHERE {
  ?e a ld:Adresse .
  ?e geo:lat ?lat . 
  ?e geo:long ?long . 
} order by ?s

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
SELECT ?e WHERE {
  ?e a ld:Adresse .
  ?e geo:lat 51.2906416 . 
  ?e geo:long 12.4391084 . 
}

Welche Geokoordinaten tauchen mehrfach auf, welche ways? -- Anfrage ist zu
komplex

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geonames: <http://www.geonames.org/ontology#> 
SELECT distinct ?lat ?long ?way count(distinct ?e) as ?s WHERE {
  ?e a ld:Adresse .
  ?e geo:lat ?lat . 
  ?e geo:long ?long . 
  ?a geonames:nearbyFeatures ?way .
} order by desc(?s)

Welche Adressen haben keine Geokoordinaten? 

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
SELECT distinct ?e WHERE {
  ?e a ld:Adresse .
  optional {?e geo:lat ?l . }
  filter (!bound(?l))
} 

Aktuell 2361 Einträge. 

Welche Adressen haben kein ld:inStreet? 

PREFIX ld: <http://leipzig-data.de/Data/Model/>
SELECT distinct ?e WHERE {
  ?e a ld:Adresse .
  optional {?e ld:hasCity ?l .}
  filter (bound(?l))
} 

keine.

--------------------------------------------
Alle APIRefs finden

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX cal: <http://www.w3.org/2002/12/cal/ical#>
SELECT distinct ?p ?pn WHERE {
  ?pn ld:hasAPIRef ?p . 
} 

--------------------------------------------
Weitere Suchanfragen 

Alle Adressen zu einem Straßen-regex finden

PREFIX ld: <http://leipzig-data.de/Data/Model/>  
SELECT ?p WHERE {
?p a ld:Adresse .
?p rdfs:label ?l . 
filter regex(?l,"Kohlgartenstra") .
} 

Alle Orte finden, die keinen Label haben

SELECT distinct ?o ?ol WHERE {
  ?o a ld:Ort ;
  optional { ?o rdfs:label ?ol .}
  filter (!bound(?ol))
} 

Alle Datensätze mit Prädikat ?p finden, das "zusatz" case insensitiv matcht.

select ?a ?p ?b where {
?a ?p ?b .
  filter regex(?p,"zusatz","i") .
}
---------------------------------------------------
RDF Graphen aufteilen

PREFIX ld: <http://leipzig-data.de/Data/Model/>
construct { ?a a ld:Buergerverein . }
from <http://leipzig-data.de/Data/Traeger/>
where {
  ?a a ld:Traeger .
  filter regex(?a, "BV") .
}

-- Events der Inspirata auslesen

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX ical: <http://www.w3.org/2002/12/cal/ical#>
select ?d ?t ?s 
from <http://leipzig-data.de/Data/Events/>
where {
  ?a a ld:Event . 
  ?a ld:hasTag <http://leipzig-data.de/Data/Tag/Inspirata> .
  ?a rdfs:label ?t .
optional { ?a ical:dtstart ?d . }
optional { ?a ical:summary ?s . }
}

-- Events ohne rdfs:label

PREFIX ld: <http://leipzig-data.de/Data/Model/>
select ?a ?b ?t 
from <http://leipzig-data.de/Data/Events/>
where {
  ?a a ?b . 
  ?a rdfs:label ?t .
filter by regex(?t,"0") . 
}

---------------------------------------------------
Liste der Schulen 

-- Adressen checken

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 
select distinct ?a ?type ?t ?alabel ?lat ?long 
from <http://leipzig-data.de/Data/Schulen/>
from <http://leipzig-data.de/Data/Adressen/>
from <http://leipzig-data.de/Data/GeoDaten/>
where {
  ?a a ld:Schule ; rdfs:label ?t ; a ?type .
optional { ?a ld:hasAddress ?add  . }
optional { ?add rdfs:label ?alabel; geo:lat ?lat ; geo:long ?long . }
filter regex(?type, "Gymnasium")
} order by ?type ?t

--- mit CodeForLeipzig vergleichen
--- Adressen fixen

PREFIX ld: <http://leipzig-data.de/Data/Model/>
select ?a ?t ?add ?aname
from <http://leipzig-data.de/Data/KidsLE/Schulen/>
from <http://leipzig-data.de/Data/Adressen/>
where {
  ?a a ld:KidLESchule ; rdfs:label ?t .
optional { ?a ld:hasAddress ?add  . }
optional { ?add rdfs:label ?aname . }
} 

--- finde zugehörige Schule zu KidLE-Schule über Adresse

PREFIX ld: <http://leipzig-data.de/Data/Model/>
select ?a ?t ?b
from <http://leipzig-data.de/Data/KidsLE/Schulen/>
from <http://leipzig-data.de/Data/Schulen/>
where {
  ?a a ld:KidLESchule ; rdfs:label ?t .
optional { ?a ld:hasAddress ?add  . }
optional { ?b a ld:Schule; ld:hasAddress ?add  . }
} 

--- finde fehlende KidLE-Schulen

PREFIX ld: <http://leipzig-data.de/Data/Model/>
select ?a ?t 
from <http://leipzig-data.de/Data/KidsLE/Schulen/>
from <http://leipzig-data.de/Data/Schulen/>
where {
  ?a a ld:Schule ; rdfs:label ?t .
optional { ?a ld:hasAddress ?add  . }
optional { ?b a ld:KidLESchule; ld:hasAddress ?add  . }
filter (!bound(?b))
} 

--- Adressen und Geokoordinaten

PREFIX ld: <http://leipzig-data.de/Data/Model/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 
select ?a ?t ?klat ?klong ?slat ?slong 
(xsd:float(?klat)-xsd:float(?slat)) as ?deltalat 
(xsd:float(?klong)-xsd:float(?slong)) as ?deltalong
from <http://leipzig-data.de/Data/KidsLE/Schulen/>
from <http://leipzig-data.de/Data/Adressen/>
from <http://leipzig-data.de/Data/GeoDaten/>
where {
  ?a a ld:KidLESchule ; rdfs:label ?t; geo:lat ?klat; geo:long ?klong .
optional { ?a ld:hasAddress ?add  . }
optional { ?add geo:lat ?slat; geo:long ?slong . }
} 

-----------------------------------
--- Ortsteile und Adressen

PREFIX ld: <http://leipzig-data.de/Data/Model/>
select ?a ?o ?l
from <http://leipzig-data.de/Data/AdministrativeGliederung/>
from <http://leipzig-data.de/Data/OrtsteilZuAdressen/>
from <http://leipzig-data.de/Data/Adressen/>
where {
  ?a a ld:Adresse ; ld:imOrtsteil ?ot . ?o a ld:Ortsteil; ld:hasStadtRef ?ot .
optional { ?a rdfs:label ?l . }
  filter regex(?ot, "Ortsteil.03") .
} 

PREFIX ld: <http://leipzig-data.de/Data/Model/>
construct { ?a ld:inOrtsteil ?o . }
from <http://leipzig-data.de/Data/AdministrativeGliederung/>
from <http://leipzig-data.de/Data/OrtsteilZuAdressen/>
from <http://leipzig-data.de/Data/Adressen/>
where {
  ?a a ld:Adresse ; ld:imOrtsteil ?ot . ?o a ld:Ortsteil; ld:hasStadtRef ?ot .
} 



